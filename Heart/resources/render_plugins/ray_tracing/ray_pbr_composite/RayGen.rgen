#version 460

#extension GL_EXT_ray_tracing : enable
#extension GL_EXT_scalar_block_layout : enable
#extension GL_EXT_buffer_reference2 : require

#define FRAME_BUFFER_BINDING 0
#define FRAME_BUFFER_SET 0
#include "../../frame_data/FrameBuffer.glsl"

#define LIGHT_BUFFER_BINDING 1
#define LIGHT_BUFFER_SET 0
#include "../../lighting_data/LightingBuffer.glsl"

#define CLUSTER_LIGHT_INDICES_BINDING 2
#define CLUSTER_LIGHT_GRID_BINDING 3
#define CLUSTER_DATA_BINDING 4
#define CLUSTER_BUFFER_SET 0
#include "../../clustered_lighting/ClusterBuffer.glsl"

#include "Common.glsl"
#include "../../util/Misc.glsl"
#include "../../util/Colormap.glsl"
#include "../../util/Lighting.glsl"
#include "../../util/BRDF.glsl"

layout(push_constant) uniform PushConstants
{
    uint ssaoEnable;
} constants;

layout(binding = 5) uniform sampler2D newGBuffer1;
layout(binding = 6) uniform sampler2D newGBuffer2;
layout(binding = 7) uniform sampler2D newGBufferDepth;
layout(binding = 8) uniform sampler2D brdfTex;
layout(binding = 9, rgba16f) uniform writeonly image2D outColor;
layout(binding = 10) uniform sampler2D reflectionsTex;
layout(binding = 11) uniform accelerationStructureEXT tlas;
layout(binding = 12) uniform sampler2D ssao;

layout(location = 0) rayPayloadEXT HitPayload prd;

// With alpha testing
bool CheckShadowed(inout LightEvalData lightData, vec3 P, vec3 N)
{
    float tMin = 0.01;
    uint flags = gl_RayFlagsSkipClosestHitShaderEXT;
    //uint flags = gl_RayFlagsTerminateOnFirstHitEXT | gl_RayFlagsOpaqueEXT | gl_RayFlagsSkipClosestHitShaderEXT;
    vec3 shadowOrigin = P + N * 0.01;
    prd.isShadowed = true;
    traceRayEXT(tlas, flags, 0xFF, 0, 0, 0, shadowOrigin, tMin, lightData.l, lightData.dist, 0);
    return prd.isShadowed;
}

void main()
{
    ivec2 coord = ivec2(gl_LaunchIDEXT.xy);
    vec2 texCoord = (vec2(coord) + vec2(0.5)) / frameBuffer.data.screenSize.xy;

    // Skybox
    float depth = texelFetch(newGBufferDepth, coord, 0).r;
    if (depth == 1.0 - float(frameBuffer.data.reverseDepth))
        return;

    vec4 gbuf1 = texelFetch(newGBuffer1, coord, 0);
    vec4 gbuf2 = texelFetch(newGBuffer2, coord, 0);

    vec3 P = ComputeWorldPositionFromDepth(texCoord, depth, frameBuffer.data.invViewProj);
    vec3 N = gbuf2.rgb;
    vec3 V = normalize(frameBuffer.data.cameraPos.xyz - P);
    vec3 albedo = gbuf1.rgb;
    float metalness = gbuf1.a;
    float roughness = gbuf2.a;

    vec3 F0 = mix(vec3(0.04), albedo.rgb, metalness);
    vec3 diffuse = mix(albedo.rgb * (vec3(1.0) - F0), vec3(0.0), metalness);

    // Direct lighitng

    // Compute cluster indices
    // Flip the depth when appropriate so the close value is always zero
    uint clusterZ = GetClusterZIndex(
        frameBuffer.data.reverseDepth ? 1 - depth : depth,
        frameBuffer.data.clipPlanes.x,
        frameBuffer.data.clipPlanes.y,
        GET_CLUSTER_DATA().clusterScale,
        GET_CLUSTER_DATA().clusterBias
    );
    uint clusterIdx = GetClusterIndex(
        vec2(coord),
        frameBuffer.data.screenSize.xy,
        GET_CLUSTER_DATA().clusterDims,
        clusterZ
    );
    LightGrid grid = GetLightGrid(clusterIdx);
    vec3 finalContribution = vec3(0.f);
    for (uint i = 0; i < grid.lightCount; i++)
    {
        uint lightIndex = GetGridLightIndex(grid.offset, i) + 1;

        LightEvalData data;
        if (GET_LIGHT(lightIndex).lightType == LIGHT_POINT)
            GetPointLightEvalData(data, GET_LIGHT(lightIndex), P, N);
        else if (GET_LIGHT(lightIndex).lightType == LIGHT_DIRECTIONAL)
            GetDirectionalLightEvalData(data, GET_LIGHT(lightIndex), N);

        if (data.nDotL <= 0)
            continue;

        if (CheckShadowed(data, P, N))
            continue;

        finalContribution += EvaluateLightBRDF(data, N, V, F0, diffuse, roughness);
    }
    
    // Indirect lighting
    {
        vec3 R = reflect(-V, N);
        vec3 F = FresnelSchlickRoughness(max(dot(N, V), 0.0), F0, roughness);

        vec3 kS = F;
        vec3 kD = 1.0 - kS;
        kD *= 1.0 - metalness;

        vec3 irradiance = vec3(0.015); // TODO: GI
        vec3 prefilteredColor = textureLod(reflectionsTex, texCoord, 0.0f).rgb;

        vec3 diffuse = irradiance * albedo;
        vec2 brdf = texture(brdfTex, vec2(max(dot(N, V), 0.0), roughness)).rg;
        vec3 specular = prefilteredColor * (F * brdf.x + brdf.y);

        float occlusion = 1.f;
        if (constants.ssaoEnable == 1)
            occlusion = texture(ssao, texCoord).r;
        finalContribution += (kD * diffuse + specular) * occlusion;
    }

    //finalContribution = textureLod(reflectionsTex, texCoord, 0.0f).rgb;
    //finalContribution = TurboColormap(float(grid.lightCount) / 100);

    imageStore(outColor, coord, vec4(finalContribution, 1.f));
}
